
export densityof
export logdensityof

export density_def

# TODO: Do we need this method?
density_def(Î¼, Î½::AbstractMeasure, x) = exp(logdensity_def(Î¼, Î½, x))

density_def(Î¼, x) = exp(logdensity_def(Î¼, x))

"""
    logdensity_rel(m1, m2, x)

Compute the log-density of `m1` relative to `m2` at `x`. This function checks
whether `x` is in the support of `m1` or `m2` (or both, or neither). If `x` is
known to be in the support of both, it can be more efficient to call
`unsafe_logdensity_rel`. 
"""
@inline function logdensity_rel(Î¼::M, Î½::N, x::X) where {M,N,X}
    T = unstatic(
        promote_type(
            return_type(logdensity_def, (Î¼, x)),
            return_type(logdensity_def, (Î½, x)),
        ),
    )
    inÎ¼ = insupport(Î¼, x)
    inÎ½ = insupport(Î½, x)
    inÎ¼ || return convert(T, ifelse(inÎ½, -Inf, NaN))
    inÎ½ || return convert(T, Inf)

    return unsafe_logdensity_rel(Î¼, Î½, x)
end

"""
    unsafe_logdensity_rel(m1, m2, x)

Compute the log-density of `m1` relative to `m2` at `x`, assuming `x` is
known to be in the support of both `m1` and `m2`.

See also `logdensity_rel`.
"""
@inline function unsafe_logdensity_rel(Î¼::M, Î½::N, x::X) where {M,N,X}
    if static_hasmethod(logdensity_def, Tuple{M,N,X})
        return logdensity_def(Î¼, Î½, x)
    end
    Î¼s = basemeasure_sequence(Î¼)
    Î½s = basemeasure_sequence(Î½)
    cb = commonbase(Î¼s, Î½s, X)
    # _logdensity_rel(Î¼, Î½)
    isnothing(cb) && begin
        Î¼ = Î¼s[end]
        Î½ = Î½s[end]
        @warn """
        No common base measure for
            $Î¼
        and
            $Î½

        Returning a relative log-density of NaN. If this is incorrect, add a
        three-argument method
            logdensity_def($Î¼, $Î½, x)
        """
        return NaN
    end
    return _logdensity_rel(Î¼s, Î½s, cb, x)
end

# Note that this method assumes `Î¼` and `Î½` to have the same type
function logdensity_def(Î¼::T, Î½::T, x) where {T}
    if Î¼ === Î½
        return zero(logdensity_def(Î¼, x))
    else
        return logdensity_def(Î¼, x) - logdensity_def(Î½, x)
    end
end

@generated function _logdensity_rel(
    Î¼s::TÎ¼,
    Î½s::TÎ½,
    ::Tuple{StaticInt{M},StaticInt{N}},
    x::X,
) where {TÎ¼,TÎ½,M,N,X}
    sÎ¼ = schema(TÎ¼)
    sÎ½ = schema(TÎ½)

    q = quote
        $(Expr(:meta, :inline))
        â„“ = logdensity_def(Î¼s[$M], Î½s[$N], x)
    end

    for i in 1:M-1
        push!(q.args, :(Î”â„“ = logdensity_def(Î¼s[$i], x)))
        # push!(q.args, :(println("Adding", Î”â„“)))
        push!(q.args, :(â„“ += Î”â„“))
    end

    for j in 1:N-1
        push!(q.args, :(Î”â„“ = logdensity_def(Î½s[$j], x)))
        # push!(q.args, :(println("Subtracting", Î”â„“)))
        push!(q.args, :(â„“ -= Î”â„“))
    end

    push!(q.args, :(return â„“))
    return q
end

"""
    logdensityof(m::AbstractMeasure, x) 

Compute the log-density of the measure `m` at `x`. Density is always relative,
but `DensityInterface.jl` does not account for this. For compatibility with
this, `logdensityof` for a measure is always implicitly relative to
[`rootmeasure(x)`](@ref rootmeasure). 

`logdensityof` works by first computing `insupport(m, x)`. If this is true, then
`unsafe_logdensityof` is called. If `insupport(m, x)` is known to be `true`, it
can be a little faster to directly call `unsafe_logdensityof(m, x)`. 

To compute log-density relative to `basemeasure(m)` or *define* a log-density
(relative to `basemeasure(m)` or another measure given explicitly), see
`logdensity_def`. 

To compute a log-density relative to a specific base-measure, see
`logdensity_rel`. 
"""
@inline function logdensityof(Î¼::AbstractMeasure, x)
    result = dynamic(unsafe_logdensityof(Î¼, x))
    ifelse(insupport(Î¼, x) == true, result, oftype(result, -Inf))
end

export unsafe_logdensityof

# https://discourse.julialang.org/t/counting-iterations-to-a-type-fixpoint/75876/10?u=cscherrer
"""
    unsafe_logdensityof(m, x)

Compute the log-density of the measure `m` at `x` relative to `rootmeasure(m)`.
This is "unsafe" because it does not check `insupport(m, x)`.

See also `logdensityof`.
"""
@inline function unsafe_logdensityof(Î¼::M, x) where {M}
    â„“_0 = logdensity_def(Î¼, x)
    b_0 = Î¼
    Base.Cartesian.@nexprs 10 i -> begin  # 10 is just some "big enough" number
        b_{i} = basemeasure(b_{i - 1}, x)
        if b_{i} isa typeof(b_{i - 1})
            return â„“_{i - 1}
        end
        â„“_{i} = let Î”â„“_{i} = logdensity_def(b_{i}, x)
            â„“_{i - 1} + Î”â„“_{i}
        end
    end
    return â„“_10
end

"""
    rebase(Î¼, Î½)

Express `Î¼` in terms of a density over `Î½`. Satisfies
```
basemeasure(rebase(Î¼, Î½)) == Î½
density(rebase(Î¼, Î½)) == ğ’¹(Î¼,Î½)
``` 
"""
rebase(Î¼, Î½) = âˆ«(ğ’¹(Î¼, Î½), Î½)

export density_rel

@inline density_rel(Î¼, Î½, x) = exp(logdensity_rel(Î¼, Î½, x))

export logdensity_rel

@inline return_type(f, args::Tuple) = Core.Compiler.return_type(f, Tuple{typeof.(args)...})
