abstract type AbstractDensity end

@inline DensityKind(::AbstractDensity) = IsDensity()

"""
    struct Density{M,B}
        Œº::M
        base::B
    end

For measures Œº and ŒΩ with Œº‚â™ŒΩ, the density of Œº with respect to ŒΩ (also called
the Radon-Nikodym derivative dŒº/dŒΩ) is a function f defined on the support of ŒΩ
with the property that for any measurable a ‚äÇ supp(ŒΩ), Œº(a) = ‚à´‚Çê f dŒΩ.
    
Because this function is often difficult to express in closed form, there are
many different ways of computing it. We therefore provide a formal
representation to allow comptuational flexibilty.
"""
struct Density{M,B} <: AbstractDensity
    Œº::M
    base::B
end

export ùíπ

"""
    ùíπ(Œº::AbstractMeasure, base::AbstractMeasure)

Compute the Radom-Nikodym derivative of Œº with respect to `base`.
"""
function ùíπ(Œº::AbstractMeasure, base::AbstractMeasure)
    return Density(Œº, base)
end

logdensityof(d::Density, x) = logdensity_rel(d.Œº, d.base, x)

logdensity_def(d::Density, x) = logdensityof(d, x)

"""
    struct DensityMeasure{F,B} <: AbstractMeasure
        density :: F
        base    :: B
    end

A `DensityMeasure` is a measure defined by a density with respect to some other
"base" measure 
"""
struct DensityMeasure{F,B} <: AbstractMeasure
    f::F
    base::B
end

function Pretty.tile(Œº::DensityMeasure{F,B}) where {F,B}
    result = Pretty.literal("DensityMeasure ‚à´(")
    result *= Pretty.pair_layout(Pretty.tile(Œº.f), Pretty.tile(Œº.base); sep = ", ")
    result *= Pretty.literal(")")
end

densitymeasure(f, base) = _densitymeasure(f, base, DensityKind(f))

_densitymeasure(f, base, ::IsDensity) = DensityMeasure(f, base)

function _densitymeasure(f, base, _)
    @error """
    The first argument of `DensityMeasure`" must be `::IsDensity`. To pass a
    function, first wrap it in `DensityInterface.funcdensity` or
    `DensityInterface.logfuncdensity`. 
    """
end

@inline function insupport(d::DensityMeasure, x)
    ifelse(insupport(d.base, x), logdensityof(d.f, x) > -Inf, false)
end

basemeasure(Œº::DensityMeasure) = Œº.base

logdensity_def(Œº::DensityMeasure, x) = logdensityof(Œº.f, x)

density_def(Œº::DensityMeasure, x) = densityof(Œº.f, x)


export ‚à´

"""
    ‚à´(f, base::AbstractMeasure)

Define a new measure in terms of a density `f` over some measure `base`.
"""
‚à´(f::Function, base::AbstractMeasure) = DensityMeasure(funcdensity(f), base)

‚à´(f, base::AbstractMeasure) = _densitymeasure(f, base, DensityKind(f))

# ‚à´(Œº::AbstractMeasure, base::AbstractMeasure) = ‚à´(ùíπ(Œº, base), base)

export ‚à´exp

"""
    ‚à´exp(f, base::AbstractMeasure)

Define a new measure in terms of a log-density `f` over some measure `base`.
"""
‚à´exp(f::Function, Œº) = ‚à´(logfuncdensity(f), Œº)

# TODO: `density` and `logdensity` functions for `DensityMeasure`

@inline function logdensityof(Œº, x)
    t() = dynamic(unsafe_logdensityof(Œº, x))
    f() = -Inf
    ifelse(insupport(Œº, x), t, f)()
end

export unsafe_logdensityof

# https://discourse.julialang.org/t/counting-iterations-to-a-type-fixpoint/75876/10?u=cscherrer
@inline function unsafe_logdensityof(Œº::M, x) where {M}
    ‚Ñì_0 = logdensity_def(Œº, x)
    b_0 = Œº
    Base.Cartesian.@nexprs 10 i -> begin  # 10 is just some "big enough" number
        b_{i} = basemeasure(b_{i-1})
        if b_{i} isa typeof(b_{i-1})
            return ‚Ñì_{i-1}
        end
        ‚Ñì_{i} = let Œî‚Ñì_{i} = logdensity_def(b_{i}, x)
            ‚Ñì_{i-1} + Œî‚Ñì_{i}
        end
    end
    return ‚Ñì_10
end

export density_rel

@inline density_rel(Œº, ŒΩ, x) = exp(logdensity_rel(Œº, ŒΩ, x))

@inline function logdensity_rel(Œº::M, ŒΩ::N, x::X) where {M,N,X}
    insupport(Œº, x) || begin
        insupport(ŒΩ, x) || return NaN
        return -Inf
    end
    insupport(ŒΩ, x) || return Inf

    Œº_depth = basemeasure_depth(Œº)
    ŒΩ_depth = basemeasure_depth(ŒΩ)

    Œîdepth = Œº_depth - ŒΩ_depth

    if Œîdepth > 0
        (‚Ñì‚Çä, Œº_0) = logdensity_steps(Œº, x, Œîdepth)
        ‚Ñì‚Çã = zero(‚Ñì‚Çä)
        ŒΩ_0 = ŒΩ
    elseif Œîdepth < 0
        (‚Ñì‚Çã, ŒΩ_0) = logdensity_steps(ŒΩ, x, -Œîdepth)
        ‚Ñì‚Çä = zero(‚Ñì‚Çã)
        Œº_0 = Œº
    else
        ‚Ñì‚Çä = ‚Ñì‚Çã = 0.0
        Œº_0 = Œº
        ŒΩ_0 = ŒΩ
    end

    @assert basemeasure_depth(Œº_0) == basemeasure_depth(ŒΩ_0)

    Base.Cartesian.@nexprs 10 i -> begin  # 10 is just some "big enough" number
        if Œº_{i-1} == ŒΩ_{i-1}
            return ‚Ñì‚Çä - ‚Ñì‚Çã
        elseif static_hasmethod(logdensity_def, Tuple{typeof(Œº_{i-1}), typeof(ŒΩ_{i-1}), typeof(x)})
            return ‚Ñì‚Çä - ‚Ñì‚Çã + logdensity_def(Œº_{i-1}, ŒΩ_{i-1}, x)
        end

        ‚Ñì‚Çä += logdensity_def(Œº_{i-1}, x)
        ‚Ñì‚Çã += logdensity_def(ŒΩ_{i-1}, x)
        Œº_{i} = basemeasure(Œº_{i-1})
        ŒΩ_{i} = basemeasure(ŒΩ_{i-1})
    end
       
    @warn """
    No common base measure for
        $Œº
    and
        $ŒΩ

    Returning a relative log-density of NaN. If this is incorrect, add a
    three-argument method
        logdensity_def(Œº, ŒΩ, x)
    """
    return NaN
end

logdensity_steps(Œº, x, ::StaticInt{0}) = (zero(logdensity_def(Œº, x)), Œº)

@inline function logdensity_steps(Œº, x, ::StaticInt{n}) where {n}
    ‚Ñì_0 = logdensity_def(Œº, x)
    b_0 = Œº
    
    Base.Cartesian.@nexprs 10 i -> begin  # 10 is just some "big enough" number
        i > n && begin
            return (‚Ñì_{i-1}, b_{i-1})
        end
        b_{i} = basemeasure(b_{i-1})
        ‚Ñì_{i} = let Œî‚Ñì_{i} = logdensity_def(b_{i}, x)
            ‚Ñì_{i-1} + Œî‚Ñì_{i}
        end       
    end
    return (‚Ñì_10, b_10)
end

@inline function _logdensity_rel(Œ±::A, Œ≤::B, x::X, ‚Ñì) where {A,B,X}
    if static_hasmethod(logdensity_def, Tuple{A,B,X})
        return ‚Ñì + logdensity_def(Œ±, Œ≤, x)
    elseif static_hasmethod(logdensity_def, Tuple{B,A,X})
        return ‚Ñì + logdensity_def(Œ≤, Œ±, x)
    else
        @warn """
        No method 
        logdensity(::$A, ::$B, ::$X)
        """
        return oftype(‚Ñì, NaN)
    end
end

export densityof
export logdensityof

export density_def

density_def(Œº, ŒΩ::AbstractMeasure, x) = exp(logdensity_def(Œº, ŒΩ, x))
density_def(Œº, x) = exp(logdensity_def(Œº, x))

"""
    rebase(Œº, ŒΩ)

Express `Œº` in terms of a density over `ŒΩ`. Satisfies
```
basemeasure(rebase(Œº, ŒΩ)) == ŒΩ
density(rebase(Œº, ŒΩ)) == ùíπ(Œº,ŒΩ)
``` 
"""
rebase(Œº, ŒΩ) = ‚à´(ùíπ(Œº,ŒΩ), ŒΩ)